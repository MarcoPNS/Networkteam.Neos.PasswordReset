prototype(Networkteam.Neos.PasswordReset:FormHandler) < prototype(Neos.Neos:ContentComponent) {

    renderer = Neos.Fusion:Case {
        requestAccepted {
            condition = ${request.arguments.resetEmail}
            renderer = Networkteam.Neos.PasswordReset:Components.Atoms.RequestAccepted
        }

        resetSucceeded {
            condition = ${request.arguments.resetSuccess && request.arguments.resetSuccess == 'true'}
            renderer = Networkteam.Neos.PasswordReset:Components.Atoms.ResetSucceeded
        }

        resetError {
            condition = ${request.arguments.resetSuccess && request.arguments.resetSuccess == 'false'}
            renderer = afx`
                <Networkteam.Neos.PasswordReset:Components.Atoms.ResetError />
                <Networkteam.Neos.PasswordReset:Components.Atoms.ResetForm />
            `
        }

        loginAfterResetFailed {
            condition = ${request.arguments.loginSuccess && request.arguments.loginSuccess == 'false'}
            renderer = Networkteam.Neos.PasswordReset:Components.Atoms.LoginError
        }

        resetForm {
            condition = ${request.arguments.token}
            renderer = Networkteam.Neos.PasswordReset:Components.Atoms.ResetForm
        }

        changeForm {
            condition = ${Security.hasRole('Networkteam.Neos.FrontendLogin:MemberArea')}
            renderer = Networkteam.Neos.PasswordReset:Components.Atoms.ChangeForm
        }

        requestForm {
            condition = ${true}
            renderer = Networkteam.Neos.PasswordReset:Components.Atoms.RequestForm {
                token = ${request}
            }
            @position = 'end'
        }
    }

    @cache {
        mode = 'uncached'
        context {
            1 = 'request'
            2 = 'node'
            3 = 'documentNode'
            4 = 'site'
        }
    }
}